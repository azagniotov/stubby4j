// https://blog.gradle.org/jcenter-shutdown
// jcenter has/is shutting down, therefore the following
// legacy plugin application config is forcing Gradle to look in Maven
buildscript {
    repositories {
        mavenLocal()
        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
    }
    dependencies {
        classpath "org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:3.0.1"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.36.0"
        classpath "com.bmuschko:gradle-nexus-plugin:2.3"
    }
}

apply plugin: "java-library"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "jacoco"
apply plugin: "org.unbroken-dome.test-sets"
apply plugin: "com.bmuschko.nexus"
apply plugin: "com.github.ben-manes.versions"


defaultTasks 'clean', 'test', 'integrationTest', 'functionalTest', 'build'
description = 'Gradle configuration for stubby4j'

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api "org.eclipse.jetty:jetty-server:${jettyVersion}"
    api "org.eclipse.jetty:jetty-servlets:${jettyVersion}"
    api "org.ehcache:ehcache:3.9.1"
    api "commons-cli:commons-cli:1.4"
    api "org.yaml:snakeyaml:1.28"
    api "org.skyscreamer:jsonassert:1.5.0"
    api "org.xmlunit:xmlunit-core:${xmlUnitVersion}"
    api "org.xmlunit:xmlunit-placeholders:${xmlUnitVersion}"
    api "io.github.azagniotov:collection-type-safe-converter:1.0.1"
    api "org.slf4j:slf4j-api:1.7.30"

    testImplementation "junit:junit:4.13.1"
    testImplementation "org.mockito:mockito-core:3.3.3"
    testImplementation "com.google.truth:truth:1.1.2"

    // https://github.com/googleapis/google-http-java-client/issues/167#issuecomment-571987053
    testImplementation "com.google.http-client:google-http-client-apache-v2:1.38.1"
}

apply from: "$rootDir/conf/gradle/ide.gradle"
apply from: "$rootDir/conf/gradle/multi-test-source-sets.gradle"
apply from: "$rootDir/conf/gradle/jacoco.gradle"
apply from: "$rootDir/conf/gradle/artifacts.gradle"
apply from: "$rootDir/conf/gradle/sonatype.gradle"

// There are some tweaks done to this task by bmuschko/gradle-nexus-plugin that in
// addition to the expected destination: ~/.m2/repository/io/github/azagniotov/stubby4j/version/,
// it also installs stubby4j artifacts into ~/.m2/repository/stubby4j, which is wrong
task installToMavenLocal(type: Upload, dependsOn: [assemble, install]) {
    repositories.mavenInstaller {
        configuration = configurations.archives
        version = "$stubbyProjectVersion"
        pom.artifactId = "$stubbyProjectName"
        pom.groupId = "$stubbyProjectGroup"
        pom.packaging = "jar"
    }
}
